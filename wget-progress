#!/usr/bin/perl
use v5.12;

use Getopt::Long 2.24 qw(GetOptions :config gnu_getopt require_order);
GetOptions
	"timeout0=i" => \(my $Timeout0 = 30),
	"timeout1=i" => \(my $Timeout1 = 20),
	"progress0=i" => \(my $Progress0 = 1<<19),
	"progress1=i" => \(my $Progress1 = 1<<20),
	"kill-after|k=i" => \(my $kill_after = 3)
		or die "getopt failed";

my $todu = shift;

my $this = -s $todu;
my $next = $this + $Progress0;

my $pid = fork // die "fork failed";
if ($pid == 0) {
	exec @ARGV;
	die "cannot exec $ARGV[0]";
}

$SIG{ALRM} = sub {
	$this = -s $todu;
	if ($this < $next) {
		warn "termnating $ARGV[0]";
		kill TERM => $pid;
		$SIG{ALRM} = sub {
			warn "killing $ARGV[0]";
			kill KILL => $pid;
		};
		alarm $kill_after;
	}
	else {
		$next = $this + $Progress1;
		alarm $Timeout1;
	}
};

alarm $Timeout0;
wait;

exit 0 if $? == 0;
exit $? >> 8 if $? >> 8;
exit 255;
