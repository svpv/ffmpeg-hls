#!/usr/bin/perl
use v5.12;

use Getopt::Long 2.24 qw(GetOptions :config gnu_getopt require_order);
GetOptions
	"timeout0=i" => \(my $Timeout0 = 30),
	"timeout1=i" => \(my $Timeout1 = 20),
	"progress0=i" => \(my $Progress0 = 1<<19),
	"progress1=i" => \(my $Progress1 = 1<<20),
	"files-from=s" => \my $files_from
		or die "getopt failed";

my @todu;
if (defined $files_from) {
	open my $fh, "<", $files_from or die "cannot open $files_from";
	@todu = <$fh>;
	chomp @todu;
}
else {
	@todu = shift;
}

sub du () {
	my $size;
	for my $arg (@todu) {
		if (opendir my $fh, $arg) {
			local $_;
			while (readdir $fh) {
				next unless -f "$arg/$_";
				$size += -s _;
			}
		}
		elsif (-f $arg) {
			$size += -s _;
		}
	}
	return $size;
}

my $this = du;
my $next = $this + $Progress0;

my $pid = fork // die "fork failed";
if ($pid == 0) {
	exec @ARGV;
	die "cannot exec $ARGV[0]";
}

$SIG{ALRM} = sub {
	$this = du;
	if ($this < $next) {
		warn "killing $ARGV[0]";
		kill TERM => $pid;
	}
	$next = $this + $Progress1;
	alarm $Timeout1;
};

alarm $Timeout0;
wait;

exit 0 if $? == 0;
exit $? >> 8 if $? >> 8;
exit 255;
