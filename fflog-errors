#!/usr/bin/perl
# Attribute ffmpeg errors and warnings to .ts segments that triggered them.
# Edits the log inplace, transforming it into "<segments.ts> <warning>" lines.
# Also prints the total stream duration to stdout.
use v5.12;
my ($part, @w, $time);
sub flush ($) {
    my $n = shift or return;
    for (@w) {
	if ($n == 1) {
	    next if /^Input #/;
	    next if /^Output #/;
	    next if /^Stream mapping/;
	    next if /^Press\s/;
	    next if /^\s/;
	}
	if ($n == -1) {
	    $time = $2,
	    next if /^(frame|size)=.* time=(\S+)/;
	    next if /^video:.* audio:/;
	}
	s/ @ 0x[0-9a-f]+\]/]/;
	s/\b(pts|dts|size)=\d+\b/$1=42/g;
	s/\b(dts to muxer in stream \d): \d+ >= \d+/$1/;
	say "$part\t$_";
    }
}
{
    my $n;
    local $^I = "";
    while (<>) {
	chomp;
	if (/^\[.*Opening '([^']+)' for reading/) {
	    flush $n++;
	    $part = $1;
	    undef @w;
	}
	elsif ($part and /\w/) {
	    push @w, $_;
	}
    }
    die "no parts" unless $n;
    flush -1;
}
{
    die "no time" unless $time;
    say $time;
}
